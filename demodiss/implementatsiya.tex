\chapter{Implementation}
As the objective of this project was to build a server, I developed a server program (\code{server.ml}). I used the Psi client software to help develop the server, but for later testing a more automatable client was needed. For this reason, I also developed a basic XMPP client controller (\code{client.ml}).
These both make heavy use of the \code{Xml} and \code{Xmpp} modules.

\section{The \code{Xml} module}\label{sec:xmlmod}
XML is central to XMPP. In my code, it comes in three main forms: as text, as low-level `Raw' XML, and as high-level \code{xml_node}. The \code{Xml} module provides utilities for working with these representations.

Both explicit and default prefixes (Section~\ref{sec:namespaces}) are used throughout XMPP. When starting out, I assumed that the particular prefix used on some element would not matter. For instance, the \code{<body>} element of HTML could be written \code{<html:body>} (as long as the \code{html} prefix referred to the HTML namespace), \code{<body>} (provided HTML was the default namespace), or even \code{<quux:body>} (if \code{quux} referred to the HTML namespace). However, XMPP provides at least one exception to this: the initial \code{<stream>} element needs to use the \code{stream:} prefix. So, a distinction between namespaces and their prefixes was preserved at some level of the system. However, in the other 99\% of cases, I considered it sufficient to place constraints only on the namespace and tag, hence why the XML checking functions are prefix-agnostic.

XML-as-text is suitable only for human comprehension; computationally, being a long unstructured block of bytes, it is unsuitable for computer processing. XMPP, though, is built around sending and receiving XML via text, so the ability to parse incoming XML and output XML text was fundamental. The \code{Xml.P} module contains Angstrom parsers for XML syntax, producing a `literal' abstract representation defined in \code{Xml.P.Raw}. Functions for converting this back into text, for output purposes, also live in \code{Xml.P.Raw}.

The main Angstrom parser is \code{tree}, which converts nested opening/closing tags and embedded text into a \code{Raw.Branch} node, or \code{Raw.Text} if the whole thing consists of just text. This suffices for most of XMPP, but there are some situations that do not involve fully completed trees. For example, setting up the client-server XML stream involves only the opening tag of the \code{<stream>} element; likewise, terminating the stream requires the closing tag. Thus, I included \code{tag_open} and \code{tag_close}. In addition, before opening the stream, an XML declaration \code{<?xml version="1.0"?>} is needed, which uses slightly different syntax to XML tags. Since this syntax does not appear anywhere else, I used a custom parser for this purpose.

These parsers are all implemented in terms of smaller named parsers, combined using the Angstrom parser combinators. For example, I defined \code{tag_open} this way:

\begin{ocaml}
let tag_open =
  tok_langle *> qual_name >>= fun (ns,id) ->
    lift2 (fun attrs _ -> Raw.Branch ((ns,id,attrs),[]))
      (many attr_val)
      tok_rangle
\end{ocaml}

This can be read as: ``Accept the token \code{<}, then a qualified name, then zero or more attribute-value pairs, then the token \code{>}; combine the qualified name and the attributes into a \code{Branch} node with no children''. Further, \code{tok_langle = lex (char '<')}, \code{tok_colon = lex (char ':')}, and so on, where \code{lex} skips whitespace.

The parsers written in this manner resemble the Context-Free-Grammars of XML structures, although the choice operator \code{<|>} is an ordered choice. This shows itself in my implementation of \code{tree}:

\begin{ocaml}
let tree = fix ( fun t_rec ->
  ( tok_langle *> qual_name >>= fun (ns,id) ->
          lift2 (fun attrs children -> Raw.Branch ((ns, id, attrs), children))
            (many attr_val)
            (tok_leaf *> return [] <|> tok_rangle *> branch t_rec (ns,id)) )
  <|> (take_while1 (function | '<' -> false | _ -> true) >>| Raw.text) )
\end{ocaml}

As an XML node can be simple text, this is accounted for by assuming text content when an attempt to parse a tag fails.

\subsection{The \code{Raw} module}
As mentioned, the \code{Raw} module contains the `literal' abstract XML representation that these parsers target. Consider

\begin{xml}[label={lst:xmlsample}]
<foo:error type="fatal" xmlns:foo="long-application-namespace" >
  <a></a>
  <b></b>
  Sample text
</foo:error>
\end{xml}

The parent node has a qualified tag \code{foo:error}, along with a list of attributes. Each attribute conceptually consists of a prefix, name and value. The prefix might be empty, such as the case of \code{type="fatal"}. The first task of abstract representation is to make all of these explicit, representing attributes as pairs of qualified names and values. \code{type="fatal"} and \code{xmlns:foo="bar"} become the list
\begin{lstlisting}
[ (("", "type"), "fatal"); (("xmlns", "foo"), "bar") ]
\end{lstlisting}
The above XML source would parse into the following \code{Raw.xml}:

\begin{ocaml}
Branch ( ("foo", "error", [
    (("", "type"), "fatal");
    (("xmlns", "foo"), "long-application-namespace")
  ]) , [
    Branch ( ("", "a", []) , []) ;
    Branch ( ("", "b", []) , []) ;
    Text "Sample text" ;
] )
\end{ocaml}

Note the `literal' interpretation of the attribute list as an actual OCaml list. Technically, the order of XML attributes does not matter, but it is simpler to use a list at this level. Also, the special considerations of namespaces and prefixes are not handled at this level; it sees \code{xmlns:foo="bar"} as just another attribute.

The \code{Raw} module provides functions to convert its data structures into strings. The main \code{to_string} function, which recursively serialises an XML tree, is as follows:

\begin{ocaml}
let rec to_string = function
  | Text str -> str
  | (Branch ((ns,tag,attrs),children)) as xml ->
    match children with
    | [] -> to_string_single (ns,tag,attrs)
    | _  -> let interior = String.concat "" (List.map to_string children) in
            to_string_open xml ^ interior ^ to_string_close xml
\end{ocaml}

An XML node without children can be written as \code{<node></node>}. However, it is legal to write such an empty node as \code{<node />}, which is often visually cleaner. This is what \code{to_string_single} does. Otherwise, the children are recursively serialised and enveloped in the opening and closing tags.

\subsubsection{Utility constructors}
Initially, having the \code{Raw} representation and its serialisation functions, I simply used them to generate the server responses. However, many elements needed at least a default namespace, all prefixes needed to be explicitly specified, and the syntax was very heavy to write and read:

\begin{ocaml}
respond_tree
  (X.Xml (("stream","features",[]),[
    X.Xml (("","mechanisms",[(("","xmlns"),Xmpp.sasl)]),[
        X.Xml (("","mechanism",[]),[ X.Text "PLAIN" ]);
        X.Xml (("","required",[]),[])
      ])
  ]));
\end{ocaml}

After consideration, I reasoned that it would be easier in the long run to add some conveniences for such construction of XML trees in OCaml code. Firstly, I wanted to avoid writing prefixes. The tags mostly use the default (empty) prefix, and the only attribute prefix that is ever used is \code{xmlns:}. By handling namespaces in a more convenient fashion, I could avoid having to specify prefixes for both tags and attributes altogether.

For the purposes of output, I committed to using only one, standard prefix per namespace (recall that prefixes are local to parts of a tree, but such freedom is not really needed here). I bundled namespaces and prefixes together, so that I could refer to them by a simple OCaml identifier. For example, the opening \code{<stream:stream>} element uses the stream prefix, referring to the namespace \code{http://etherx.jabber.org/streams}. I made definitions (in the \code{Xmpp} module) like so:

\begin{ocaml}
let jstream  = ("stream","http://etherx.jabber.org/streams")
let jclient  = ("client","jabber:client")
let jserver  = ("server","jabber:server")
\end{ocaml}

Other namespaces were only ever used with the default (empty) prefix. I did the same with them:

\begin{ocaml}
let streams = ("","urn:ietf:params:xml:ns:xmpp-streams")
let session = ("","urn:ietf:params:xml:ns:xmpp-session")
let sasl    = ("","urn:ietf:params:xml:ns:xmpp-sasl")
let bind    = ("","urn:ietf:params:xml:ns:xmpp-bind")
...
\end{ocaml}

I developed convenience functions to construct XML trees using this: \code{xml_d}, \code{xml}, \code{xml_n}, and \code{text}.

For \code{xml_n}, the \code{n} stands for ``no namespace''. This is used for child XML that does not use namespaces at all. For example, the following:
\begin{ocaml}
xml_n "rectangle" [ "width", "240"; "height", "180" ] [
  text "Sample text"
]
\end{ocaml} constructs the \code{Raw.xml}
\begin{ocaml}
Raw.Xml (("","rectangle",[(("","width"),"240"); (("","height"),"180")]),[
  Raw.Text "Sample text"
]
\end{ocaml}

\code{xml_d} means `declare'. It is similar to \code{xml_n}, but accepts a prefix-namespace pair. It applies the prefix to the tag and declares the association between prefix and tag in the attributes, automatically. This means that example \ref{lst:xmlsample} could be constructed like so:

\begin{ocaml}
module Xmpp = struct
  ...
  let foo = ("foo", "long-application-namespace")
  ...
end

xml_d Xmpp.foo [ "type", "fatal" ] [
  xml_n "a" [] [];
  xml_n "b" [] [];
  text "Sample text"
]
\end{ocaml}

Finally, \code{xml} is a somewhat idiosyncratic variant that exists for child XML that can use a prefix, but does not declare it.

The advantages of this notation are clear: high signal-to-noise ratio\footnote{OCaml's data constructors are not curried, requiring parentheses, whereas function application does not require them. Also, in some situations, such as lists, it is legal to drop parentheses in pairs: \code{[ (x,y); (z,w) ]} becomes \code{[ x,y; z,w ]}.}, and the use of functions allows automatic handling of namespaces. The end result is that the code finally looks somewhat like the thing it represents.

\subsection{High-level \code{xml_node}}
The higher-level representation, \code{xml_node}, exists for situations where \code{Raw}'s data structures become unwieldy. It is defined as follows:

\begin{ocaml}
type xml_node =
| Text of lang * string
| Xml of {
  tag    : qname;
  attr   : string -> string option;
  attr_full : qname -> string option;
  namespace : string -> string option;
  lang  : lang;
  child : xml_node list;
  orig  : P.Raw.xml;
}
\end{ocaml}

The main improvement is the use of an opaque attribute lookup function, rather than an explicit data structure. \code{attr_full} takes a qualified name, but since most attributes do not have a namespace prefix, the more convenient attr is also provided. \code{attr} is implemented as calling \code{attr_full} with an empty namespace prefix.

One consideration with namespaces is that prefix-namespace mappings are inherited to child nodes of their declaration site. \code{xml_node} incorporates this functionality into the namespace function. It finds the namespace associated with a prefix, delegating to the parent node if unsuccessful.

\code{attr}, \code{attr_full} and namespace all internally use the standard library's \code{Map} structure, being more appropriate for lookup than lists; lists require a linear scan to locate a mapping, and a full scan of the list to determine its absence, whereas \code{Map} provides logarithmic-time lookup.

An \code{xml_node} is constructed from an original \code{Raw.xml} structure, and it is useful to keep this information for output purposes. It is stored in the \code{orig} field.

The language of any text content is, like namespaces, an inherited special attribute, and is signified by \code{xml:lang} in the attribute list. It is stored in the \code{lang} field of the branch, and propagated to leaf \code{Text} nodes. This is because a \code{Text} instance, in isolation, does not have a reference to its parent, and hence must have its own \code{lang} attribute.

The construction of an \code{xml_node} from \code{Raw.xml} is accomplished by the \code{from_raw_br} function. It recursively walks the tree, building up \code{attr} and \code{namespace} maps along the way, and processes special attributes.

\subsubsection{Semantic checking and matching} \label{sec:matchers}
The text-to-\code{Raw} parsers solve the problem of structuring XML and detecting syntax errors; after all, not all text is valid XML. However, XMPP is a protocol over XML and hence imposes a second level of `syntax' on top of this. So, in a similar way, not all valid XML is valid XMPP. But this does not entail further structuring of the XML; rather, it mainly involves checking that certain tags or attributes are present, and extracting values of attributes or embedded text nodes --- this problem is one of ``pattern matching'' rather than parsing. OCaml's built-in pattern matching proved not to be suitable for this purpose [and would not work with the high-level rep anyway; perhaps I should prioritise this reason], so I went down the route of parsers and combinators modelled after Angstrom.

Unlike Angstrom's parsers, which are designed to be fairly orthogonal fundamental building blocks of more complex parsers, mine are deliberately special-purpose. I went with whatever would be useful to write readable code, without necessarily being exhaustive in the range of behaviours achievable by combining them.

I viewed a `matcher' as a function accepting an \code{xml_node} and returning a \code{Result} value. Composition of matchers was then to involve both composition of functions, and composition of \code{Result} values\footnote{The resulting library could be seen as a fusion of the Function and \code{Result} monads.}. Application of a matcher to an \code{xml_node} is simple function application.

The matchers live in the \code{Xml.Check} module. The main ones are \code{tag} / \code{qtag}, \code{attr} / \code{attr_opt}, \code{attv}, \code{child} / \code{children}, \code{orig} and \code{text}.

\begin{itemize}
  \item \code{tag "message"} succeeds if the node has the \code{message} tag

  \item \code{qtag} functions like \code{tag}, but takes a namespace prefix to also check

  \item \code{attr "id"} results in the value of the \code{id} attribute if it exists

  \item \code{attv "id" "1234"} succeeds if the \code{id} attribute exists and is equal to 1234

  \item \code{attr_opt} is like \code{attr} but does not fail, instead resulting in \code{Some <value>} or \code{None}

  \item \code{child} gives the first child of the node if it exists

  \item \code{children} gives a list of the node's children (possibly the empty list, so this one also never fails)

  \item \code{orig} results in the low-level \code{Raw} representation from which the \code{xml_node} was obtained

  \item \code{text} gives the text string of a \code{Text} node
\end{itemize}

I avoid using the word `return' here, as strictly what these functions return is a \code{Result}, possibly containing the `resulting' value --- or an error. For those that merely check some condition rather than retrieve a value (\code{tag}/\code{qtag} and \code{attv}), I say they `succeed' instead\footnote{This could mean `resulting' in the value \code{()}, but I actually found it more useful to `result' in the \code{xml_node} passed in.}.

\subsubsection{Matcher combinators}
The combinators let me write XML matching code resembling, as closely as possible in OCaml\footnote{Short of syntax extensions perhaps, which I am unfamiliar with.}, the XML itself:

\begin{ocaml}
tag "iq" *> attv "type" "get" *> attr "id" >>= fun id ->
  (* id is now bound to the "id" attribute *)
  ...
\end{ocaml}

This would succeed on the XML node
\begin{xml}
<foo:iq type="get" id="123" > ... </foo:iq>.
\end{xml}

I implemented the usual \code{>>=} for `bind', \code{*>} for `sequence', \code{<|>} for `alternative' and the direction-reversed versions where needed.

\begin{itemize}
  \item \code{(f >>= fun x -> g) xml} first performs \code{f xml}; if successful, it binds the result value to \code{x} and returns \code{g xml}.\footnote{In other words, \code{(f >>= g) xml} evaluates to \code{g xml x} if \code{f xml} results in \code{x} --- I only explained it with an explicit binder because that is how it tends to be used.} Otherwise, if \code{f xml} fails, it aborts with the error value.

  \item \code{(f *> g) xml} is like \code{>>=} but discards the result, binding no values. In this sense, it sequences a `check' with a check or match, although this is a conceptual distinction that is not enforced in the code.

  \item \code{(f <|> g) xml} is like \code{*>}, but with opposite error behaviour: when \code{f xml} fails, \code{g xml} is tried.
\end{itemize}

The matcher implementations are all fairly straightforward and similar to each other. For instance, \code{attr}:

\begin{ocaml}
let attr k = function
  | Xml xml -> (match xml.attr k with
    | Some v -> Ok v
    | None -> Error (format
        "Expected %s attribute in <%s> tag"
          k (snd xml.tag)) )
  | Text _ -> Error "Expected XML, not content"
\end{ocaml}

Out of the above matchers, only \code{text} makes sense for text nodes. Because both the low- and high-level XML types incorporate the possibility of text, all matchers except \code{text} have to account for this case at runtime.

\subsubsection{The question of buffering}
Angstrom provides the \code{parse_only} function for parsing an in-memory string in its entirety. But in this application, such a mode is not possible. First, from a remote source on the network, completed XML might arrive in several bursts of data rather than all at once. Second, most XML takes the form of stanzas, which need to be parsed and processed one-by-one as they come. This all points to some sort of `incremental' parsing functionality, requiring buffering of XML from outside. Fortunately, Angstrom's \code{parse} function produces a state that can be progressively fed input until it finishes.

From the beginning, I found it useful to employ an ``expect-input, respond-output'' model for the conversational bulk of the server and client code, such as the initial handshake. Functions for responding to the client were simple, generally consisting of output to a channel plus a flush of the channel. The `expect' function, though, was more involved.

Angstrom's \code{Buffered} interface maintains an internal buffer, of type \code{Bigstring}. After a successful parse, some of the data in the buffer may be unconsumed. It is the caller's responsibility to ensure parsing continues where it left off, and this is handled by the function passing the relevant sub-buffer each time it is needed. When everything has been consumed, the buffer needs refilling from the network. Unfortunately, the standard library's \code{input} function reads data into a \code{Bytes}-type buffer and not a \code{Bigstring}. To bridge this gap, I split my originally desired 1024-byte buffer into a \code{Bytes} and \code{Bigstring} of 512 bytes each. \code{input} reads into the \code{Bytes}, which is then blitted to the \code{Bigstring}, which can then be passed to Angstrom for parsing.

The user-level \code{expect} function takes an Angstrom parser (usually \code{P.tree}) and tries to parse from these internal network-backed buffers. It returns a \code{Result} value, and is often used in tandem with the high-level XML functions like so:

\begin{ocaml}
expect P.tree >>| Xml.from_raw >>= fun high_level_xml_node -> ...
\end{ocaml}

\section{The \code{Xmpp} module}
This module is for common functionality more specific to XMPP than XML, although the line is arbitrary. The main functionality here is roster management, and recognition of stanzas. As mentioned in Section~\ref{sec:xmlmod}, the prefix-namespace pairs used often in XMPP are defined here.

\subsection{The Roster}
There is conceptually a mapping from user to their roster. I found it useful to view a user's roster as another mapping, from a contact's JID to their \code{<item>} record. The user-roster association is a global \code{Map} data structure, and the roster itself is another \code{Map}.

I represented \code{<item>} records, in-memory, as follows:

\hspace*{-\parindent}%
\begin{minipage}{\linewidth}
  \begin{ocaml}
    type item = {
      jid     : string;        (* Contact's Jabber ID                       *)
      name    : string;        (* Human-friendly name of contact            *)
      recv_ok : bool;          (* OK to receive presence from this contact? *)
      send_ok : bool;          (* OK to send presence to this contact?      *)
      groups  : string list; (* Membership of contact in groups           *)
    }
  \end{ocaml}
\end{minipage}

This is almost a direct XML-to-OCaml translation of XMPP's \code{<item>} element, the main difference being my treatment of the \code{subscription} attribute. I felt that the \code{subscription} attribute as \code{none}, \code{from}, \code{to} or \code{both} was an odd and hard-to-remember way of representing subscription. The system thus translates this field into two boolean conditions: \code{recv_ok} (activated by \code{to} and \code{both}) and \code{send_ok} (activated by \code{from} and \code{both}). This was better for the uses to which the roster is put: when wishing to send presence notifications, I simply check whether it is OK to send, via \code{send_ok}, rather than mentally decrypting `to' or `from'.

Rosters are managed through the \code{Roster} sub-module. After authenticating a client, the server calls \code{Roster.load_from_storage} with their username, to ensure that their contact information is available.

\code{Roster.load_from_storage} uses Angstrom parsers and the high-level \code{xml_node} interface to load the roster file into memory. The only part of this that is slightly interesting is \code{item_of_xml}, which constructs an item record from what should be an \code{<item>} XML node:

\begin{ocaml}
let item_of_xml =
  let open Xml.Check in
  tag "item" *> attr "jid" >>= fun jid ->
    attr "name" >>= fun name ->
      attr_opt "subscription" >>= ( function
      | None        -> pure (false,false)
      | Some "none" -> pure (false,false)
      | Some "to"   -> pure (true,false)
      | Some "from" -> pure (false,true)
      | Some "both" -> pure (true,true)
      | _ -> fail "Unknown subscription type" ) >>=
  fun (recv_ok,send_ok) ->
    pure { jid; name; recv_ok; send_ok; groups=[] }
\end{ocaml}

This demonstrates the utility of the \code{xml_node} matchers discussed in Section~\ref{sec:matchers}. This function treats the absence of a \code{subscription} attribute as equivalent to \code{subscription="none"} (hence the use of \code{attr_opt}). \code{groups} is set to an empty value for the aforementioned reasons. There is also a corresponding \code{to_xml} function acting on \code{item} structures.

There is one final thing to mention about how rosters are handled: the function \code{Roster.get_subs_of} returns a list of all the subscribers of a user, filtered from their full roster. If their roster does not exist, a value of \code{None} is returned instead, rather than an error. When this function is used to broadcast a client's initial presence later on, the absence of a roster is treated as a subscription to and from everybody currently online. This was included as a default option to make bulk testing easier, as it precludes the need to maintain lots of identical roster files that achieve the same thing.

\subsection{The \code{Stanza} module}
The main purpose of this module is to make the common task of handling stanzas easier, by bringing some of the semantics of XMPP stanzas into the OCaml type level. Although there are three types of stanzas in XMPP, I only found it necessary to give \code{iq} stanzas this treatment, since they make use of request IDs and have structure conditional on their attributes. The \code{message} and \code{presence} stanzas only required the checking of attributes and forwarding, so they do not feature here.

I defined the type \code{Iq.t} as a combination of request ID and \code{iq_type}. \code{iq_type} incorporates the \code{type} attribute of the \code{iq} stanza and the various possibilities for the children of the node:

\begin{ocaml}
  type query = Xml.xml_node
  type iq_type =
  | Get of query
  | Set of query
  | Result of query option
  | Error of query option * error
\end{ocaml}

XMPP specifies that \code{type="get"} or \code{"set"} means the contents are a query for the information. \code{result} \code{iq}s can be empty (as acknowledgements of success) and likewise with errors, and this is mapped to the OCaml \code{option} type.

The \code{of_xml} function converts XML to an \code{Iq.t}, again using the matchers:

\begin{ocaml}
let of_xml =
let open Xml.Check in
  tag "iq" *> attr "id" >>= fun req_id ->
    attr "type" >>= (function
    | "get" -> child >>| fun ch -> Get ch
    | "set" -> child >>| fun ch -> Set ch
    | "result" -> fun xml -> Ok (Result (Rresult.R.to_option (child xml)))
    | "error" -> fun xml -> Ok (Error (Rresult.R.to_option (child xml),"?"))
    | _ -> fail "bad-request") >>| fun iq_type -> { req_id; iq_type }
\end{ocaml}

The module gives rise to the following idiom that is employed for received \code{iq} stanzas, taking advantage of OCaml's pattern matching on function arguments:

\begin{ocaml}
  expect P.tree >>| Xml.from_raw >>= Xmpp.Stanza.Iq.of_xml >>=
  fun { req_id; iq_type=(Get query) } -> ...
\end{ocaml}

\section{The server program}
\subsection{Work-dispatching machinery (the \code{Dispatch} module)}\label{sec:mod-dispatch}
This module concerns itself with providing a means to push stanzas for delivery to work queues, and to retrieve them from said work queues, concurrently.

On a per-client basis, after handshaking (discussed later), the server sets up a worker thread to forward stanzas to the client. The worker obtains stanzas by means of \code{dequeue_work} from the work queue created by the \code{client_connected} function. The main thread then reacts to messages and other stanzas that arrive, forwarding them to the appropriate worker by means of \code{dispatch}.

A global map exists to obtain the work-queue for a specified JID. This map uses the immutable \code{Map} data structure in tandem with an OCaml \code{ref}erence cell which, being concurrently updated, is protected with a mutex.

Each work-queue consists of a \code{Queue} data structure, along with a \code{Condition} variable \code{avail} and its associated mutex, acting as a \emph{monitor} for the queue. \code{avail} is signalled when \code{dispatch} pushes work to the queue, indicating that there is available work to process. There is also a boolean representing `finished', which exists to allow the worker thread to quit gracefully.

The \code{dequeue_work} function blocks, waiting for the \code{avail} signal. If the `finished' flag is set, it hands the value \code{None} to the worker once the queue empties. This lets the worker finish any work it currently has before exiting. Otherwise, work is extracted from the queue and returned.

Finally, when the main thread's message-processing loop is over, it calls \code{client_disconnected}. This sets the worker's `finished' flag, signals \code{avail}, and removes the client's entry from the global map.

\subsection{Server operation}
The server program uses a simple `driver' to spawn off a thread each time a client connects. The function \code{sv_start ()} simply performs \code{Driver.establish_server}\footnote{The standard library provides \code{Unix.establish_server}, which forks a child process to handle each client connection. I implemented a similar function that spawns threads instead of processes. This is necessary for the shared data structures in the \code{Dispatch} and \code{Roster} modules to work.} on the local loopback address, passing in the function \code{per_client}.

When \code{Driver.establish_server} accepts a client connection, it opens a read and a write channel and passes them to \code{per_client}, invoked on a new thread. The XMPP protocol then follows in two broad stages, \emph{setup} and \emph{service}. Setup involves handshaking with the client, performing authentication and resource binding, and the initial presence. Service consists of responding to client requests and delivering messages to the client.

All of these stages make use of the \code{Result} monad to deal with errors. Errors that occur during handshaking abort the protocol, as do fundamental errors during service (such as malformed XML). The service stage also deals with certain recoverable errors in individual stanzas, such as requests for unimplemented functionality.

\subsubsection{Setup stage}\label{sec:server-setup}
After a client connects via TCP, the XML streams between it and the server are negotiated. The server expects the \code{<stream>} opening element from the client (after an XML declaration), and responds with its own. It looks like this:

\begin{ocaml}
let stream_handshake id =
  expect A.(P.xml_decl *> P.tag_open) >>| Xml.from_raw >>=
    Xml.Check.(qtag Xmpp.jstream "stream" *> attr "to") >>= fun my_addr ->
  let response = Raw.(to_string_open (xml_d Xmpp.jstream "stream" [
    "xmlns", snd Xmpp.jclient;
    "version", "1.0"; "from", my_addr; "id", id;
  ] []))
  in respond response; Ok (my_addr, id)
\end{ocaml}

This makes use of all the main concepts from the \code{Xml} module. The \code{expect} function is passed a composite Angstrom text parser to recognise the XML declaration followed by the opening \code{<stream>} element. A successful result is converted to a high-level \code{xml_node}, which is then checked for the correct tag, and the relevant data is extracted. A similar response follows, constructed via the helper functions in \code{Xml.Raw}, giving the client a stream ID to use\footnote{Though the stream IDs given by the server are not random, they do not have an effect on the behaviour of Psi or the test clients.}. The function returns the information obtained: the stream ID, and the server's Web address (\code{ptii.proj} is used throughout this project).

The rest of the setup implements the points in Section~\ref{sec:prep-handshake}. The server offers the list of stream features to negotiate, consisting solely of the \code{PLAIN} authentication mechanism. After client chooses \code{PLAIN}, login information is extracted from the base64-encoded text with a special Angstrom parser.

Now that the username has been obtained, the JID is constructed, and is used to load the client's roster into memory if it exists. A second handshake is performed, resource binding occurs, and the client's full JID is returned---incorporating the username, server address, and resource. An IQ `set' session request is expected, to formally establish the session, and is answered with an empty IQ, signalling success.

The initial roster request, is then serviced with any roster data that was loaded previously. If there is none, the server responds with an empty roster. This is technically not reflective of ``no roster'' being treated specially as a universal subscription, to / from everyone. However, this behaviour is to make testing easier.

Finally, the initial presence is received, echoed and forwarded to the user's subscribers. If universally subscribed, the server obtains list of currently online clients from the \code{Dispatch} module, filtering out the originator from the list before forwarding (since the presence was already echoed back).

At this point, setup is finished, now moves into servicing incoming stanzas.

\subsubsection{Service stage}
The server now calls \code{Dispatch.client_connected} to obtain a work queue. It sets up a worker thread, which loops, extracting from the queue and forwarding stanzas to the client.

Next, the main thread itself enters a loop, expecting stanzas and behaving accordingly. Auxiliary behaviour such as parsing the stanzas and handling errors lives in the loop body, while the main behaviour is separated into functions \code{handle_iq}, \code{handle_presence}, and \code{handle_message}:

\begin{itemize}
  \item \code{handle_iq} uses the IQ's request ID to return an \code{Iq.t} representing the stanza error \code{feature-not-implemented}. The calling code translates this into the actual XML and pushes this to the work queue.

  \item \code{handle_presence} watches for the final \code{<presence type="unavailable" />} that signifies the client's wish to disconnect, and returns a value that the calling code uses to end the loop.

  \item \code{handle_message} ensures \code{<message>} elements have a valid source and destination, returning the message and the recipient to deliver it to. The calling code then uses \code{Dispatch.dispatch} to send it on its way.
\end{itemize}

Once the loop finishes, \code{Dispatch.client_disconnected} is called to cut off the stream of stanzas into the worker thread. The main thread then waits for the worker to process what remains in its queue before terminating. Finally, the closing \code{</stream:stream>} is sent and the socket is closed as \code{per_client} finishes. This stage can actually be reached prematurely if stream errors occur during setup or service, and if so the error is returned and printed out.

\section{The \code{Client} module}\label{sec:mod-client}
Psi was very useful for using XMPP and learning about the protocol, but it could only be operated through its GUI. To evaluate the server's performance, I needed automatable clients that I could control \emph{en masse}. This was the motivation for the \code{Client} module.

Since each client has substantial state (sockets, buffers, XMPP session), I reasoned it would be easier to operate clients as objects in the interactive toplevel than to use separate records and functions. The module defines a \code{client} class, whose objects contain the following methods:

\begin{itemize}
  \item Constructor \code{client} is called with the username and the server address; it connects, and stores the username for later.

  \item \code{#handshake} method performs handshake and returns the roster. The handshake here is designed to be symmetric to the server's, to ensure it all goes smoothly. It is not designed to anticipate substantial variation in the server responses, as it is purely for testing.

  \item \code{#message "recpt" body} sends the body XML via a \code{message} stanza to \code{recpt@ptii.proj}.

  \item \code{#message_t "recpt" text} is a more user-friendly version where the body consists of text. The text is wrapped in a \code{<body>} element, without which Psi does not display the message.

  \item \code{#disconnect} sends \code{<presence type="unavailable" >} and the closing \code{</stream:stream>}.

  \item \code{#spill} extracts one XML element / tree from client's receive buffer, and returns it.
\end{itemize}
